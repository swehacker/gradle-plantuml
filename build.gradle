plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
}

sourceCompatibility = 10

group = 'swehacker.gradle.plugin.plantuml'
version = '0.1.0-SNAPSHOT'

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile 'org.reflections:reflections:0.9.11'

    compile gradleApi()
    compile localGroovy()

    testCompile gradleTestKit()
    testCompile 'org.assertj:assertj-core:3.11.0'
    testCompile 'junit:junit:4.12'
}

gradlePlugin {
    plugins {
        plantuml {
            id = 'plantuml'
            implementationClass = 'swehacker.gradle.plugin.plantuml.PlantUmlPlugin'
        }
    }
}

// Write the plugin's classpath to a file to share with the tests
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files sourceSets.main.runtimeClasspath
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }

    repositories {
        maven {
            name = 'myRepo'
            url = "file://${buildDir}/repo"
        }
    }
}